plugins {
    id "java"
    id "maven-publish"
    id "jacoco"
    id "org.sonarqube" version "7.0.1.6134"
}

def environment = new Properties()
def environmentFile = file(".env")

if (environmentFile.exists())
    environmentFile.withReader("UTF-8") { reader -> environment.load(reader) }

def repositoryUrl = (environment.getProperty("REPOSITORY_URL") != null ? environment.getProperty("REPOSITORY_URL") : System.getenv("REPOSITORY_URL"))
def repositoryUserName = (environment.getProperty("REPOSITORY_USERNAME") != null ? environment.getProperty("REPOSITORY_USERNAME") : System.getenv("REPOSITORY_USERNAME"))
def repositoryPassword = (environment.getProperty("REPOSITORY_PASSWORD") != null ? environment.getProperty("REPOSITORY_PASSWORD") : System.getenv("REPOSITORY_PASSWORD"))

def sonarToken = (environment.getProperty("SONAR_TOKEN") != null ? environment.getProperty("SONAR_TOKEN") : System.getenv("SONAR_TOKEN"))
def sonarOrganization = (environment.getProperty("SONAR_ORGANIZATION") != null ? environment.getProperty("SONAR_ORGANIZATION") : System.getenv("SONAR_ORGANIZATION"))
def sonarProjectKey = sonarOrganization + "_" + rootProject.name

jacoco {
    toolVersion = "0.8.13"
}

group = "${buildPackage}"
version = "${buildVersion}"

sonar {
    properties {
        property("sonar.token", "${sonarToken}")
        property("sonar.projectKey", "${sonarProjectKey}")
        property("sonar.organization", "${sonarOrganization}")
        property 'sonar.exclusions', "src/test/**"
    }
}

repositories {
    mavenLocal()
	mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs("src/main/java")
        }
	    resources {
    	    srcDirs("src/main/resources")
    	}
    }
    test{
        java {
            srcDirs("src/test/java")
        }
	    resources {
    	    srcDirs("src/test/resources")
    	}
    }
}

dependencies {
    implementation("com.sun.activation:jakarta.activation:2.0.1")
    implementation("org.apache.activemq:activemq-broker:5.19.1")
    implementation("org.apache.activemq:activemq-client:5.19.1")
    implementation("net.sourceforge.barbecue:barbecue:1.5-beta1")
    implementation("net.bytebuddy:byte-buddy:1.18.2")
    implementation("commons-codec:commons-codec:1.20.0")
    implementation("org.apache.commons:commons-dbcp2:2.14.0")
    implementation("org.apache.commons:commons-fileupload2-jakarta-servlet6:2.0.0-M4")
    implementation("org.apache.commons:commons-jexl3:3.6.1")
    implementation("commons-net:commons-net:3.12.0")
    implementation("org.apache.commons:commons-text:1.15.0")
    implementation("org.dom4j:dom4j:2.2.0")
    implementation("com.sun.mail:dsn:2.0.2")
    implementation("org.ehcache:ehcache:3.11.1")
    implementation("com.sun.mail:gimap:2.0.2")
    implementation("com.google.guava:guava:32.0.1-jre")
    implementation("com.h2database:h2:2.4.240")
    implementation("org.hibernate:hibernate-core:5.6.15.Final")
    implementation("org.hibernate:hibernate-ehcache:5.6.15.Final")
    implementation("org.apache.httpcomponents:httpcore:4.4.16")
    implementation("org.apache.httpcomponents:httpcore-nio:4.4.16")
    implementation("org.apache.httpcomponents:httpclient:4.5.14")
    implementation("org.apache.httpcomponents:httpmime:4.5.14")
    implementation("com.sun.mail:imap:2.0.2")
    implementation("com.sun.mail:jakarta.mail:2.0.2")
    implementation("com.itextpdf:itextpdf:5.5.13.4")
    implementation("com.fasterxml.jackson.core:jackson-annotations:2.20")
    implementation("com.fasterxml.jackson.core:jackson-core:2.20.1")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.20.1")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:2.20.1")
    implementation("net.sf.jasperreports:jasperreports:6.21.5")
    implementation("com.google.zxing:javase:3.5.4")
    implementation("net.sf.jmimemagic:jmimemagic:0.1.5")
    implementation("jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.1")
    implementation("org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1")
    implementation("org.apache.logging.log4j:log4j-api:2.25.3")
    implementation('org.apache.logging.log4j:log4j-core:2.25.3')
    implementation("com.mysql:mysql-connector-j:9.5.0")
    implementation("com.squareup.okhttp3:okhttp:5.3.2")
    implementation("com.squareup.okio:okio:3.16.4")
    implementation("org.apache.poi:poi:5.5.1")
    implementation("org.apache.poi:poi-ooxml:5.5.1")
    implementation("com.sun.mail:pop3:2.0.2")
    implementation("pull-parser:pull-parser:2.1.10")
    implementation("com.sun.mail:smtp:2.0.2")
    implementation("com.github.ua-parser:uap-java:1.6.1")
    implementation("jakarta.servlet.jsp:jakarta.servlet.jsp-api:4.0.0")
    implementation("jakarta.servlet:jakarta.servlet-api:6.1.0")

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.mockito:mockito-core:5.21.0")
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
    options.deprecation = true
    options.release = 21
}

tasks.withType(Jar).configureEach {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	includeEmptyDirs = false
    zip64 = true
}

tasks.withType(ProcessResources).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    includeEmptyDirs = false
}

tasks.register("allJar", Jar) {
    archiveClassifier = "all"

    from(sourceSets.main.output)
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

tasks.register("modelJar", Jar) {
	archiveClassifier = "model"

    from(sourceSets.main.output) {
		include("**/model/**")
	}
}

tasks.register("controllerJar", Jar) {
	archiveClassifier = "controller"

	from(sourceSets.main.output) {
		include("**/controller/**")
	}
}

tasks.register("persistenceJar", Jar) {
	archiveClassifier = "persistence"

	from(sourceSets.main.output) {
		include("**/persistence/**")
	}
}

tasks.register("serviceJar", Jar) {
	archiveClassifier = "service"

	from(sourceSets.main.output) {
		include("**/service/**")
	}
}

tasks.register("uiJar", Jar) {
	archiveClassifier = "ui"

	from(sourceSets.main.output) {
		include("**/ui/**")
	}
}

tasks.register("resourcesJar", Jar) {
	archiveClassifier = "resources"

    from(sourceSets.main.output) {
		include("etc/**/*.properties")
		include("etc/**/*.xml")
		include("etc/**/*.xsd")
	}
}

tasks.register("coreJar", Jar) {
	archiveClassifier = "core"

	from(sourceSets.main.output) {
        exclude("**/model/**")
        exclude("**/controller/**")
        exclude("**/persistence/**")
        exclude("**/service/**")
        exclude("**/ui/**")
        exclude("etc/**/*.properties")
        exclude("etc/**/*.xml")
        exclude("etc/**/*.xsd")
	}
}

javadoc.options.addStringOption("Xdoclint:none", "-quiet")

javadoc {
    destinationDir = file("docs")
}

tasks.register("javadocJar", Jar) {
	archiveClassifier = "javadoc"

	from(javadoc)
}

test {
    finalizedBy(jacocoTestReport)
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
        csv.required.set(false)
    }
}

artifacts {
    archives(allJar)
    archives(coreJar)
    archives(resourcesJar)
    archives(controllerJar)
	archives(modelJar)
	archives(persistenceJar)
	archives(serviceJar)
	archives(uiJar)
    archives(javadocJar)
}

publishing{
    publications {
        mavenJava(MavenPublication) {
            artifact(jar)
            artifact(allJar)
            artifact(coreJar)
            artifact(resourcesJar)
            artifact(modelJar)
            artifact(persistenceJar)
            artifact(serviceJar)
            artifact(controllerJar)
            artifact(uiJar)
            artifact(javadocJar)
        }
    }
    repositories {
        maven {
            url = "${repositoryUrl}"

            credentials {
                username = "${repositoryUserName}"
                password = "${repositoryPassword}"
            }
        }
    }
}