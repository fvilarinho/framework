plugins {
    id "java"
    id "maven-publish"
    id "jacoco"
    id "org.sonarqube" version "7.0.1.6134"
    id "war"
}

def environment = new Properties()
def environmentFile = file(".env")

if (environmentFile.exists())
    environmentFile.withReader("UTF-8") { reader -> environment.load(reader) }

def buildPackage = (environment.getProperty("BUILD_PACKAGE") != null ? environment.getProperty("BUILD_PACKAGE") : System.getenv("BUILD_PACKAGE"))
def buildName = (environment.getProperty("BUILD_NAME") != null ? environment.getProperty("BUILD_NAME") : System.getenv("BUILD_NAME"))
def buildVersion = (environment.getProperty("BUILD_VERSION") != null ? environment.getProperty("BUILD_VERSION") : System.getenv("BUILD_VERSION"))

def frameworkRepositoryUrl = (environment.getProperty("FRAMEWORK_REPOSITORY_URL") != null ? environment.getProperty("FRAMEWORK_REPOSITORY_URL") : System.getenv("FRAMEWORK_REPOSITORY_URL"))
def frameworkRepositoryUserName = (environment.getProperty("FRAMEWORK_REPOSITORY_USERNAME") != null ? environment.getProperty("FRAMEWORK_REPOSITORY_USERNAME") : System.getenv("FRAMEWORK_REPOSITORY_USERNAME"))
def frameworkRepositoryPassword = (environment.getProperty("FRAMEWORK_REPOSITORY_PASSWORD") != null ? environment.getProperty("FRAMEWORK_REPOSITORY_PASSWORD") : System.getenv("FRAMEWORK_REPOSITORY_PASSWORD"))

def repositoryUrl = (environment.getProperty("REPOSITORY_URL") != null ? environment.getProperty("REPOSITORY_URL") : System.getenv("REPOSITORY_URL"))
def repositoryUserName = (environment.getProperty("REPOSITORY_USERNAME") != null ? environment.getProperty("REPOSITORY_USERNAME") : System.getenv("REPOSITORY_USERNAME"))
def repositoryPassword = (environment.getProperty("REPOSITORY_PASSWORD") != null ? environment.getProperty("REPOSITORY_PASSWORD") : System.getenv("REPOSITORY_PASSWORD"))

def sonarUrl = (environment.getProperty("SONAR_URL") != null ? environment.getProperty("SONAR_URL") : System.getenv("SONAR_URL"))
def sonarToken = (environment.getProperty("SONAR_TOKEN") != null ? environment.getProperty("SONAR_TOKEN") : System.getenv("SONAR_TOKEN"))
def sonarOrganization = (environment.getProperty("SONAR_ORGANIZATION") != null ? environment.getProperty("SONAR_ORGANIZATION") : System.getenv("SONAR_ORGANIZATION"))
def sonarProjectKey = sonarOrganization + "_" + rootProject.name

jacoco {
    toolVersion = "0.8.13"
}

group = "${buildPackage}"
version = "${buildVersion}"

sonar {
    properties {
        property("sonar.url", "${sonarUrl}")
        property("sonar.token", "${sonarToken}")
        property("sonar.projectKey", "${sonarProjectKey}")
        property("sonar.organization", "${sonarOrganization}")
        property("sonar.exclusions", "**/test/*")
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "${frameworkRepositoryUrl}"

        credentials {
            username = "${frameworkRepositoryUserName}"
            password = "${frameworkRepositoryPassword}"
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs("src/main/java")
        }
        resources {
            srcDirs("src/main/resources")
        }
    }
    test{
        java {
            srcDirs("src/test/java")
        }
        resources {
            srcDirs("src/test/resources")
        }
    }
}

dependencies {
    implementation("br.com.concepting:framework:3.10.17:all")

    annotationProcessor("br.com.concepting:framework:3.10.17:all")

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.mockito:mockito-core:5.21.0")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.deprecation = true
    options.release = 21
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    includeEmptyDirs = false
    zip64 = true
}

tasks.withType(ProcessResources).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    includeEmptyDirs = false
}

tasks.register("generateCode", JavaCompile) {
    source(sourceSets.main.java.srcDirs)

    destinationDirectory = compileJava.destinationDirectory

    classpath = sourceSets.main.runtimeClasspath + configurations.runtimeClasspath + configurations.annotationProcessor

    options.annotationProcessorPath = sourceSets.main.runtimeClasspath + configurations.runtimeClasspath + configurations.annotationProcessor
    options.compilerArgs = [
            "-proc:only",
            "-processor", "br.com.concepting.framework.processors.AnnotationProcessorFactory",
            "-AbuildDir=" + project.projectDir,
            "-AbuildPackage=" + buildPackage,
            "-AbuildName=" + buildName,
            "-AbuildVersion=" + buildVersion
    ]
}

tasks.register("allJar", Jar) {
    archiveClassifier = "all"

    from(sourceSets.main.output)
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}

tasks.register("modelJar", Jar) {
    archiveClassifier = "model"

    from(sourceSets.main.output) {
        include("**/model/**")
    }
}

tasks.register("controllerJar", Jar) {
    archiveClassifier = "controller"

    from(sourceSets.main.output) {
        include("**/controller/**")
    }
}

tasks.register("persistenceJar", Jar) {
    archiveClassifier = "persistence"

    from(sourceSets.main.output) {
        include("**/persistence/**")
    }
}

tasks.register("serviceJar", Jar) {
    archiveClassifier = "service"

    from(sourceSets.main.output) {
        include("**/service/**")
    }
}

tasks.register("uiJar", Jar) {
    archiveClassifier = "ui"

    from(sourceSets.main.output) {
        include("**/ui/**")
    }
}

tasks.register("resourcesJar", Jar) {
    archiveClassifier = "resources"

    from(sourceSets.main.output) {
        include("etc/**/*.properties")
        include("etc/**/*.xml")
        include("etc/**/*.xsd")
    }
}

tasks.register("coreJar", Jar) {
    archiveClassifier = "core"

    from(sourceSets.main.output) {
        exclude("**/model/**")
        exclude("**/controller/**")
        exclude("**/persistence/**")
        exclude("**/service/**")
        exclude("**/ui/**")
        exclude("etc/**/*.properties")
        exclude("etc/**/*.xml")
        exclude("etc/**/*.xsd")
    }
}

javadoc.options.addStringOption("Xdoclint:none", "-quiet")

javadoc {
    destinationDir = file("docs")
}

tasks.register("javadocJar", Jar) {
    archiveClassifier = "javadoc"

    from(javadoc)
}

test {
    finalizedBy(jacocoTestReport)
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
        csv.required.set(false)
    }
}

test {
    finalizedBy(jacocoTestReport)
}

jacocoTestReport {
    reports {
        html.required.set(true)
        xml.required.set(true)
        csv.required.set(false)
    }
}

war {
    from("src/main/ui")
}

publishing{
    publications{
        mavenJava(MavenPublication) {
            artifact(jar)
            artifact(allJar)
            artifact(coreJar)
            artifact(resourcesJar)
            artifact(modelJar)
            artifact(persistenceJar)
            artifact(serviceJar)
            artifact(controllerJar)
            artifact(uiJar)
            artifact(javadocJar)
        }
    }

    repositories {
        maven {
            url = "${repositoryUrl}"

            credentials {
                username = "${repositoryUserName}"
                password = "${repositoryPassword}"
            }
        }
    }
}